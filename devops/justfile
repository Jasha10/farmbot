set working-directory := '..'

publish $CARGO_REGISTRY_TOKEN:
    # $CARGO_REGISTRY_TOKEN will be set as an environment variable for the following commands.
    cargo install cargo-release
    cargo release publish --workspace --execute --no-confirm

# Bump the version in Cargo.toml and Cargo.lock, commit the changes, and tag the commit
bump:
    # check that the worktree is clean
    [ -z "$(git status --porcelain)" ]

    cargo install cargo-release
    # Update Cargo.toml and Cargo.lock
    cargo release version patch --workspace --execute --no-confirm
    # commit the changes
    cargo release commit --execute --no-confirm
    # tag the commit as 'v{VERSION}'
    # We do not pass the --workspace flag as that would create
    # a separate tag for each workspace package, e.g. stowsave-v{VERSION}, ...
    cargo release tag --execute --no-confirm

# Push branch and tag to the remote repo
push:
  # We do npt pass the --workspace flag as that would attempt to push
  # a tag per each workspace package, e.g. stowsave-v{VERSION}, ...
  cargo release push --execute --no-confirm

build:
  cargo build --all --verbose

test:
  cargo test --all --verbose
